package de.thekid.dialog.cmd;

import util.cmd.Command;
import io.File;
import io.FileUtil;
import de.thekid.dialog.IEntry;
import de.thekid.dialog.Album;
import de.thekid.dialog.AlbumChapter;
import de.thekid.dialog.AlbumImage;
import de.thekid.dialog.EntryCollection;
import de.thekid.dialog.SingleShot;
import de.thekid.dialog.ImageStrip;

import native standard.unserialize;
import native standard.sprintf;

public class ConvertSqlite extends Command {
  private string $dataLocation;

  [@arg(name= 'data')]
  public void setDataLocation(string $data= 'data/') {
    $this.dataLocation= $data;
  }

  public void run() {
    $this.out.writeLine('===> Converting dialog installation to sqlite ...');

    $i= 0;
    while (true) {
      try {
        $page= $this.getPage($i++);
      } catch (io.IOException $e) {
        break;
      }

      $this.out.writeLine('---> Importing page ' ~ $i);
      $this.importPage($page);
    }
  }

  private var getPage(int $i) {
    return unserialize(FileUtil::getContents(new File($this.dataLocation ~ 'page_' ~ $i ~ '.idx')));
  }

  private void importPage(var $page) {
    foreach ($name in $page['entries'] as string[]?) {
      $entry= $this.getEntryFor($name);

      try {
        $method= $this.getClass().getMethod(sprintf('import%s', $entry.getClass().getSimpleName()));
      } catch (lang.ElementNotFoundException $e) {
        $this.err.writeLine('!--> Skipping import of type ' ~ $entry.getClassName());
        continue;
      }

      $method.invoke($this, [$entry]);
    }

    $this.out.writeLine();
  }

  public void importAlbum(Album $album) {
    $this.out.writeLine('---> Importing album ' ~ $album.getTitle() ~ ' (' ~ $album.getName() ~ ')');

    foreach ($chapter in $album.chapters as AlbumChapter[]) {
      $this.importAlbumChapter($album, $chapter);
    }


  }

  public void importAlbumChapter(Album $album, AlbumChapter $chapter) {
    $this.out.writeLine(' --> Processing chapter ' ~ $chapter.getName());

    foreach ($image in $chapter.images as AlbumImage[]) {
      $this.importAlbumImage($image);
    }
  }

  public void importAlbumImage(AlbumImage $image) {
    $this.out.writeLine('  -> Processing image ' ~ $image.getName());
  }


  /**
   * Returns entry for a specified name
   *
   */
  public IEntry getEntryFor(string? $name) {
    return $this._getEntryFor($name, IEntry::class);
  }

  /**
   * Helper method
   *
   * @param   name
   * @param   expect expected type
   * @throws  IllegalArgumentException if found entry is not of expected type
   */
  protected inline IEntry _getEntryFor(string? $name, XPClass? $expect) throws IllegalArgumentException {
    $entry= unserialize(FileUtil::getContents(new File($this.dataLocation ~ $name ~ '.dat')));

    // Check expectancy
    if (!$expect.isInstance($entry)) throw new IllegalArgumentException(sprintf(
      'Entry of type %s found, %s expected',
      xp::typeOf($entry),
      $expect.getName() 
    ));

    return $entry;
  }
}