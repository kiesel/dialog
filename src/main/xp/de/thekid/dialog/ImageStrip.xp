package de.thekid.dialog;

import util.Date;

import native standard.sprintf;
import native standard.str_replace;

/**
 * Represents an image strip
 *
 * @test     xp://de.thekid.dialog.unittest.ImageStripTest
 * @see      xp://de.thekid.dialog.Album
 */
public class ImageStrip implements IEntry {
  public string $name           = '';
  public string $title          = '';
  public string $description    = '';
  public Date $createdAt        = null;
  public AlbumImage[] $images   = [];

  /**
   * Constructor
   */
  public __construct($this.name= null) { }

  /**
   * Name accessors
   */
  public void setName($this.name) { }
  public self withName($this.name) -> $this;
  public string getName() -> $this.name;

  /**
   * Title accessors
   */
  public void setTitle($this.title) { }
  public self withTitle($this.title) -> $this;
  public string getTitle() -> $this.title;

  /**
   * Description accessors
   */
  public void setDescription($this.description) { }
  public self withDescription($this.description) -> $this;
  public string getDescription() -> $this.description;

  /**
   * CreatedAt accessors
   */
  public void setCreatedAt($this.createdAt) { }
  public self withCreatedAt($this.createdAt) -> $this;
  public Date getCreatedAt() -> $this.createdAt;

  /**
   * Get date
   *
   * @see     xp://de.thekid.dialog.IEntry
   */
  public Date getDate() -> $this.createdAt;

  /**
   * Add an element to images
   *
   * @return  the added entry
   */
  public AlbumImage addImage(AlbumImage $image) {
    $this.images[]= $image;
    return $image;
  }

  /**
   * Add an element to images and return this image strip
   *
   */
  public self withImage(AlbumImage $image) {
    $this.images[]= $image;
    return $this;
  }
  
  /**
   * Get one image element by position. Returns NULL if the element 
   * can not be found.
   *
   */
  public AlbumImage imageAt(int $i) {
    if (!isset($this.images[$i])) return null;
    return $this.images[$i];
  }

  /**
   * Get number of images
   *
   * @return  int
   */
  public int numImages() {
    return $this.images.length;
  }

  /**
   * Find an image
   *
   */
  public var imageUrn(string $name) {
    foreach ($id, $image in $this.images) {
      if ($image.name === $name) return ['id' : $id];
    }
    return null;
  }

  /**
   * Retrieve a string representation
   *
   * @return  string
   */
  public string toString() {
    $is= '';
    foreach ($id, $image in $this.images) {
      $is~= '    ' ~ str_replace("\n", "\n  ", $this.images[$id].toString()) ~ "\n";
    }
    return sprintf("%s(%s) {\n%s  }", $this.getClassName(), $this.name, $is);
  }
}
