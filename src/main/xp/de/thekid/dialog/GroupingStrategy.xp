package de.thekid.dialog;

import util.Date;

/**
 * Grouping strategy enumeration
 *
 * - By day - Group images by day
 * - By period - By day and *N*ight, *M*orning, *A*fternoon, *E*vening.
 * - By hour - By day and hour
 */
abstract enum GroupingStrategy {
  day {
    protected string groupBy(Date $date) -> $date.toString('Y-m-d');
  },
  period {
    protected string groupBy(Date $date) {
      $time= $date.toString('H:m:s');
      if ($time < '06:00:00') {
        $period= ' N';
      } else if ($time < '12:00:00') {
        $period= ' M';
      } else if ($time < '17:00:00') {
        $period= ' A';
      } else if ($time < '21:00:00') {
        $period= ' E';
      } else {
        $period= ' N';
      }
      return $date.toString('Y-m-d') ~ $period;
    }
  },
  hour {
    protected string groupBy(Date $date) -> $date.toString('Y-m-d H');
  };

  /**
   * Returns group for a given date.
   *
   * @return  string unique group identifier
   */
  protected abstract string groupBy(Date $date);

  /**
   * Returns group for a given album image.
   *
   * @return  string unique group identifier
   */
  public string groupFor(AlbumImage $image) throws IllegalArgumentException {
    if (null === $image.exifData) {
      throw new IllegalArgumentException('Cannot determine group for ' ~ $image.toString());
    }
    return $this.groupBy($image.exifData.dateTime);
  }
}
