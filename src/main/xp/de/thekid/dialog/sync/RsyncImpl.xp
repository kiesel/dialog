/* This class is part of the XP framework
 *
 * $Id$ 
 */

package de.thekid.dialog.sync;

import util.cmd.Console;

import native standard.array_merge;

/**
 * Implements the synchronizer interface by using an external call to
 * the "rsync" utility.
 *
 * @see   http://en.wikipedia.org/wiki/Rsync
 */
public class RsyncImpl extends Syncronizer {
  protected string $target;

  /**
   * Sets target
   *
   */
  public void setTarget(string? $target) {
    $this.target= $target;
  }
  
  /**
   * Creates a string representation
   *
   */
  public string toString() {
    return $this.getClassName() ~ '(-> ' ~  $this.target ~ ')';
  }

  /**
   * Runs rsync
   *
   */
  public void run() {
    $args= array_merge([ '-v', '-r', '-e', 'ssh' ], $this.origins, [ $this.target ]);

    $command= new Process('rsync', $args, null, null);
    do {
      Console::$out.write($command.out.read());
    } while (!$command.out.eof());

    $exitCode= $command.close();
    if (0 != $exitCode) {
      throw new lang.reflect.TargetInvocationException('Invoking `' ~ $command.getCommandLine() ~ '` failed w/ exitcode ' ~ $exitCode);
    }
  }
}
