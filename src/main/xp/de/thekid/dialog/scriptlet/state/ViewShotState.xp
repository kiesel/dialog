package de.thekid.dialog.scriptlet.state;

import de.thekid.dialog.SingleShot;
import scriptlet.xml.XMLScriptletRequest;
import scriptlet.xml.XMLScriptletResponse;
import scriptlet.xml.workflow.Context;
import xml.Node;

import native standard.sscanf;
import native standard.strtolower;
import native pcre.preg_replace;

/**
 * Handles /xml/shot/view
 *
 */
public class ViewShotState extends de.thekid.dialog.scriptlet.AbstractDialogState {
  public static string[] $modes= ['color', 'gray'];

  /**
   * Process this state.
   *
   */
  public void process(XMLScriptletRequest? $request, XMLScriptletResponse? $response, Context? $context) {
    $name= $request.getURL().getValue('shot');
    $mode= $request.getURL().getValue('mode');

    with ($shot= $this._getEntryFor($name, SingleShot::class) as SingleShot?) {
      $s= $response.addFormResult(Node::fromObject($shot, 'selected'));
      $s.setAttribute('mode', self::$modes[$mode]);
      $s.setAttribute('page', $this.getDisplayPageFor($name));

      // Check topics
      if (null !== $shot.image.iptcData) {
        $topics= $s.addChild(new Node('topics'));
        foreach ($keyword in $shot.image.iptcData.getKeywords()) {
          $topics.addChild(new Node('topic', $keyword, [ name : preg_replace('/[^a-z0-9-]/', '_', strtolower($keyword)) ]));
        }
      }
    }
  }
}
